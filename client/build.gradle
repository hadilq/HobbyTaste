apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk")
                            .replace("client", "HobbyTaste"))
        }
    }

    defaultConfig {
        applicationId "ir.asparsa.hobbytaste"

        buildConfigField 'String', 'BASE_URL', '"http://192.168.1.7:8090/api/v1/"'
        buildConfigField 'int', 'MAX_FILE_SIZE', '5242880'
    }

    buildTypes {
        debug {
            multiDexEnabled true
            signingConfig signingConfigs.config
        }
        release {
            multiDexEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    debugCompile project(path: ':client-core', configuration: 'debug')
    releaseCompile(project(path: ':client-core', configuration: 'release')) {
        exclude group: 'com.android.support'
        exclude group: 'com.google.dagger'
        exclude group: 'com.jakewharton'
        exclude group: 'io.reactivex'
    }
    compile(project(':common'))
    compile "com.android.support:appcompat-v7:$global_appcompatVersion"
    compile "com.android.support:design:$global_designVersion"
    compile "com.android.support:support-v4:$global_supportVersion"
    // OrmLite
    compile "com.j256.ormlite:ormlite-core:$global_ormliteVersion"
    compile "com.j256.ormlite:ormlite-android:$global_ormliteVersion"
    // Dagger
    provided "com.google.dagger:dagger-compiler:$global_daggerVersion"
    compile "com.google.dagger:dagger:$global_daggerVersion"
    provided 'javax.annotation:jsr250-api:1.0'
    // Butter Knife
    compile "com.jakewharton:butterknife:$global_butterknifeVersion"
    provided "com.jakewharton:butterknife-compiler:$global_butterknifeVersion"
    // RxAndroid
    compile "io.reactivex:rxandroid:$global_rxandroidVersion"
    compile "io.reactivex:rxjava:$global_rxjavaVersion"
    // Google map
    compile 'com.google.android.gms:play-services:9.8.0'
    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$global_retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$global_retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$global_retrofitVersion"
    // Picasso
    compile "com.squareup.picasso:picasso:$global_picassoVersion"

    // Test
    testCompile "junit:junit:$global_junitVersion"
}
